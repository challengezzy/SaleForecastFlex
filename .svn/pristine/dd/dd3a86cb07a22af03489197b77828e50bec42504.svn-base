<?xml version="1.0" encoding="utf-8"?>
<mx:Module xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:s="library://ns.adobe.com/flex/spark" 
		   xmlns:mx="library://ns.adobe.com/flex/mx" layout="vertical" width="100%" height="100%" 
		   paddingLeft="10" paddingRight="10" paddingBottom="10"
		   creationComplete="creationCompleteHandler(event)" keyDown="dataGridKeyDownHandler(event)" 
		   xmlns:containers="aks.aksmx.containers.*" xmlns:conversion="dmdd.dmddmx.com.conversion.*" xmlns:common="dmdd.dmddmx.com.common.*">
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
		<s:RemoteObject endpoint="{ClientEnvironment.endpoint}" id="srvConversionType" destination="ConversionService">	
			<s:method name="ImConvsersionSet" result="resultHandler4ImConvsersionSet(event)" fault="faultHandler4ImConvsersionSet(event)">
			</s:method>							  
		</s:RemoteObject>			
	</fx:Declarations>
	<fx:Metadata>
		[ResourceBundle("mod_conversion")]
	</fx:Metadata>
	<fx:Script>
		<![CDATA[
			import aks.aksas.utils.UtilUi;
			
			import com.cool.assets.CoolAssetsFile;
			
			import dmdd.dmddas.common.constant.BizConst;
			import dmdd.dmddas.common.constant.SysConst;
			import dmdd.dmddas.common.system.ClientEnvironment;
			import dmdd.dmddas.common.utils.UtilExceptionLocale;
			import dmdd.dmddas.common.utils.UtilPeriod;
			import dmdd.dmddas.dataaccess.aidobject.ABImConversionSet;
			import dmdd.dmddas.dataaccess.aidobject.ABImHistoryData;
			import dmdd.dmddas.dataaccess.bizobject.BBizData;
			import dmdd.dmddas.dataaccess.bizobject.BSysDictionaryItem;
			import dmdd.dmddas.dataaccess.bizobject.BUnit;
			import dmdd.dmddas.dataaccess.bizobject.BUnitGroup;
			
			import mx.collections.ArrayCollection;
			import mx.collections.IViewCursor;
			import mx.controls.Alert;
			import mx.controls.DataGrid;
			import mx.controls.dataGridClasses.DataGridColumn;
			import mx.events.FlexEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			
			
			//	描述    begin
			//	描述    end
			
			//	0	输入参数    begin						
			//	0	输入参数    end
			
			//	1	输出参数    begin
			//	1	输出参数    end
			
			//	2	本地变量    begin	
			[Bindable]
			private var prvVar4dp4comAdgImConversionSetData:ArrayCollection = new ArrayCollection();
			[Bindable]
			private var prvVar4arrDictBizDataType:ArrayCollection = new ArrayCollection();		
			
			private var prvVar4unit:BUnit = null;
			
			private var prvVar4arrFailedRecord:ArrayCollection = new ArrayCollection();
			
			private var prvVar4arrOkButFailedRecord:ArrayCollection = new ArrayCollection();
			//	2	本地变量    end			
			
			//	3	公共方法    begin			
			//	3	公共方法    end		
			
			//	4	初始化方法    begin	
			protected function creationCompleteHandler(event:FlexEvent):void
			{
					
			}			
			//	4	初始化方法    end				
			
			//	5	事件响应方法    begin
		
			private function onClear():void
			{
				this.prvVar4dp4comAdgImConversionSetData = new ArrayCollection();
				this.comAdgImConversionSetData.columnImportResult.visible = false;
				this.btnSave.enabled = false;
				this.btnExport.enabled = false;
			}
			
			private function onSave():void
			{
				var alert_title_hint:String = resourceManager.getString('mod_conversion','alert_title_hint');
				var alert_msg:String = null;  
				if( this.prvVar4dp4comAdgImConversionSetData.length <= 0 )
				{
					alert_msg = resourceManager.getString('mod_conversion','alert_msg_nodatatosave');
					Alert.show( alert_msg, alert_title_hint );	
					return;												
				}
				
				UtilUi.pubFun4PopWaitWindow( this );
				
				this.srvConversionType.ImConvsersionSet( prvVar4dp4comAdgImConversionSetData );
			}	
			
			private function faultHandler4ImConvsersionSet(event:FaultEvent):void
			{
				UtilUi.pubFun4RemoveWaitWindow();
				Alert.show( UtilExceptionLocale.getExceptionMessageLocaleByFaultEvent( event ), resourceManager.getString('main','alt_title_warn') );
			}
			
			private function resultHandler4ImConvsersionSet(event:ResultEvent):void 
			{
				this.prvVar4dp4comAdgImConversionSetData = ArrayCollection(event.result);
				
				this.prvVar4arrFailedRecord = new ArrayCollection();
				this.prvVar4arrOkButFailedRecord = new ArrayCollection();
				var totalNum:int = 0; 
				var failNum:int = 0;
				var i:int;
				var rstImHistoryData:ABImConversionSet = null;
				if( this.prvVar4dp4comAdgImConversionSetData != null )
				{
					totalNum = this.prvVar4dp4comAdgImConversionSetData.length;
					for( i=0; i<this.prvVar4dp4comAdgImConversionSetData.length; i=i+1 )
					{
						rstImHistoryData = ABImConversionSet(this.prvVar4dp4comAdgImConversionSetData.getItemAt(i));
						if( rstImHistoryData.importResult != BizConst.IMPORT_RESULT_SUCCESS )
						{
							rstImHistoryData.importResult = UtilExceptionLocale.getExceptionMessageLocaleByCauseMessage( rstImHistoryData.importResult );
							
							this.prvVar4arrFailedRecord.addItem( rstImHistoryData );
						}
						
					}
					failNum = this.prvVar4arrFailedRecord.length;
				}
				
				UtilUi.pubFun4RemoveWaitWindow();
				this.comAdgImConversionSetData.columnImportResult.visible = true;
				
				var alert_title_hint:String = resourceManager.getString('mod_conversion','alert_title_hint');
				var alert_msg:String = null;  	
				
				alert_msg = resourceManager.getString('mod_conversion','alert_msg_totalnum');
				alert_msg = alert_msg + totalNum + "\n";
				alert_msg = alert_msg + resourceManager.getString('mod_conversion','alert_msg_failnum');
				alert_msg = alert_msg + failNum + "\n";
				
				Alert.show( alert_msg, alert_title_hint );
				
				if( failNum > 0 )
				{
					this.btnExport.enabled = true;					
				}
				else
				{
					this.btnExport.enabled = false;					
				}		
				
			}		
			private function onExport():void
			{				
				var TSVString:String = this.exportCSV( this.comAdgImConversionSetData ) ;
				// Copy the TSV string to the clipboard
				System.setClipboard(TSVString);					
			}
			
			
			//	5	事件响应方法    end
			
			//	6	本地方法    begin
			private function exportCSV(dg:DataGrid, csvSeparator:String="\t", lineSeparator:String="\n"):String
			{
				var data:String = "";
				var columns:Array = dg.columns;
				var columnCount:int = columns.length;
				var column:DataGridColumn;
				var header:String = "";
				var headerGenerated:Boolean = false;
				var dataProvider:Object = dg.dataProvider;
				var rowCount:int = dataProvider.length;
				var dp:Object = null;
				
				var cursor:IViewCursor = dataProvider.createCursor ();
				var j:int = 0;
				
				//loop through rows
				while (!cursor.afterLast)
				{
					var obj:Object = null;
					obj = cursor.current;
					
					//loop through all columns for the row
					for(var k:int = 0; k < columnCount; k++)
					{
						column = columns[k];
						//Exclude column data which is invisible (hidden)
						if(!column.visible)
						{
							continue;
						}
						
						data += "\""+ column.itemToLabel(obj)+ "\"";
						if(k < (columnCount -1))
						{
							data += csvSeparator;
						}
						//generate header of CSV, only if it's not genereted yet
						if (!headerGenerated)
						{
							header += "\"" + column.headerText + "\"";
							if (k < columnCount - 1)
							{
								header += csvSeparator;
							}
						}
						
						
					}
					
					headerGenerated = true;
					if (j < (rowCount - 1))
					{
						data += lineSeparator;
					}
					j++;
					cursor.moveNext ();
				}
				
				//set references to null:
				dataProvider = null;
				columns = null;
				column = null;
				
				return (header + "\r\n" + data);
			} 
			
			
			
			private function dataGridKeyDownHandler( event:KeyboardEvent ):void   
			{     
				if (event.ctrlKey && this.comAdgImConversionSetData.getChildByName("clipboardProxy") == null )     
				{       
					// Add an invisible TextField object to the DataGrid       
					var textFieldClipboard:TextField = new TextField();       
					textFieldClipboard.name = "clipboardProxy";       
					this.comAdgImConversionSetData.addChild(textFieldClipboard);       
					textFieldClipboard.visible = false;       
					textFieldClipboard.type = TextFieldType.INPUT;       
					textFieldClipboard.multiline = true;       
					// Populate the TextField with selected data in TSV format       
					// textField.text = getTextFromItems(dataGrid.selectedItems);       
					// textField.setSelection(0, textField.text.length - 1);       
					// Listen for textInput event       
					textFieldClipboard.addEventListener(TextEvent.TEXT_INPUT, clipboardProxyPasteHandler);    
					// Set player-level focus to the TextField       
					systemManager.stage.focus = textFieldClipboard;     
				}   
			}
			
			private function clipboardProxyPasteHandler(event:TextEvent):void   
			{     
				// Extract values from TSV format and populate the DataGrid
				if( this.comAdgImConversionSetData.columnImportResult.visible == true )
				{
					this.comAdgImConversionSetData.columnImportResult.visible = false;
				}
				
				this.prvVar4dp4comAdgImConversionSetData = new ArrayCollection();
				this.getImHistoryDatasFromText( event.text );  
				this.btnSave.enabled = true;
				
				// 一定要在这里把 textField移除，不然会报 Flash.events.event 到 indexChang eEvent 的类型转换错误
				// 移去 textField
				var textFieldClipboard:TextField = TextField(this.comAdgImConversionSetData.getChildByName("clipboardProxy"));       
				if ( textFieldClipboard != null )
				{         
					this.comAdgImConversionSetData.removeChild(textFieldClipboard);
				}   								
			}   
			
			private function getImHistoryDatasFromText(text:String):void   
			{     
				var alert_title_hint:String = resourceManager.getString('mod_conversion','alert_title_hint');
				var alert_msg:String = null;  
				
				var rstArrayImHistoryData:Array = new Array();
				var rows:Array = text.split("\n");
				if( rows == null )
				{
					alert_msg = resourceManager.getString('mod_conversion','alert_msg_noconversionsetdata');
					Alert.show( alert_msg, alert_title_hint );	
					return;	
				}     
				if (!rows[rows.length - 1])
				{       
					rows.pop();
				}     
				
				if( rows.length <= 1 )
				{
					alert_msg = resourceManager.getString('mod_conversion','alert_msg_noconversionsetdata');
					Alert.show( alert_msg, alert_title_hint );						
					return;
				}
				
				var rwHeader:String = rows[0];
				var fieldsHeader:Array = rwHeader.split( "\t" );
				if(!(fieldsHeader[fieldsHeader.length-1]))
				{
					fieldsHeader.pop();
				} 				
				if( fieldsHeader.length <  2 )
				{
					alert_msg = resourceManager.getString('mod_conversion','alert_msg_noconversionsetdata');
					Alert.show( alert_msg, alert_title_hint );						
					return;					
				}

				
				var row:String = null;
				var rowIdx:int;
				var fields:Array = null;
				var periodIdx:int;
				var periodNum:int;
				
				var newImConversionSetData:ABImConversionSet = null;
				for( rowIdx=1; rowIdx<rows.length; rowIdx=rowIdx+1 )
				{
					newImConversionSetData = new ABImConversionSet();
					newImConversionSetData.importResult = null;
					
					row = rows[rowIdx];
					fields = row.split("\t");
					if( fields.length < 2 )
					{
						continue;
					}
					newImConversionSetData.productCode = fields[0];
					newImConversionSetData.conversionTypeCode = fields[1];
					this.prvVar4dp4comAdgImConversionSetData.addItem( newImConversionSetData );
				}
			}   			
			//	6	本地方法   end	
			
		]]>
	</fx:Script>
	
	<common:LabelBoundary paddingTop="5" labelText="{resourceManager.getString('mod_conversion','conversionset_import')}" />
	<containers:AksControlBar>
		<s:Button label="{resourceManager.getString('mod_conversion','btn_save')}" click="onSave()" id="btnSave" enabled="false" icon="{CoolAssetsFile.saveIcon}"/> 
		<s:Button label="{resourceManager.getString('mod_conversion','btn_clear')}" click="onClear()" icon="{CoolAssetsFile.clearIcon}"/>
		<s:Button label="{resourceManager.getString('mod_conversion','btn_export')}" click="onExport()" id="btnExport" enabled="false" icon="{CoolAssetsFile.doctableIcon}"/>
		
	</containers:AksControlBar>
	<conversion:Com_Conversion_ImConversionSet_AdvancedDataGrid id="comAdgImConversionSetData"
									dataProvider="{this.prvVar4dp4comAdgImConversionSetData}" width="100%" height="100%"/>		
	
	
</mx:Module>
