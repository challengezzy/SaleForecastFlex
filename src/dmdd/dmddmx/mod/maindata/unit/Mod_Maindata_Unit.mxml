<?xml version="1.0" encoding="utf-8"?>
<mx:Module xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:s="library://ns.adobe.com/flex/spark" 
		   xmlns:mx="library://ns.adobe.com/flex/mx" layout="vertical" width="100%" height="100%" paddingLeft="10" paddingRight="10" paddingTop="10"
		   creationComplete="creationCompleteHandler(event)"  
		   xmlns:containers="aks.aksmx.containers.*" xmlns:controls="aks.aksmx.controls.*" xmlns:common="dmdd.dmddmx.com.common.*"
		   xmlns:unit="dmdd.dmddmx.com.unit.*" xmlns:buttons="com.cool.buttons.*">
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
		<s:RemoteObject endpoint="{ClientEnvironment.endpoint}" id="srvUnitGroup" destination="UnitService">
			<s:method name="getUnitGroupsStat" result="resultHandler4getUnitGroupsStat(event)" fault="faultHandler4getUnitGroupsStat(event)">
			</s:method>			
			<s:method name="getUnitGroups" result="resultHandler4getUnitGroups(event)" fault="faultHandler4getUnitGroups(event)">
			</s:method>
			<s:method name="delUnitGroup" result="resultHandler4delUnitGroup(event)" fault="faultHandler4delUnitGroup(event)">
			</s:method>			
		</s:RemoteObject>		
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import aks.aksas.utils.UtilUi;
			
			import com.cool.assets.CoolAssetsFile;
			
			import dmdd.dmddas.common.constant.BizConst;
			import dmdd.dmddas.common.constant.FunPermiConst;
			import dmdd.dmddas.common.constant.SkinConst;
			import dmdd.dmddas.common.system.ClientEnvironment;
			import dmdd.dmddas.common.utils.UtilExceptionLocale;
			import dmdd.dmddas.dataaccess.bizobject.BUnitGroup;
			import dmdd.dmddmx.com.unit.Com_Unit_UnitGroup_AU;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			
			//	描述    begin
			//	描述    end
			
			//	0	输入参数    begin						
			//	0	输入参数    end
			
			//	1	输出参数    begin
			//	1	输出参数    end
			
			//	2	本地变量    begin	
			[Bindable]
			private var prvVar4dp4comAdgUnitGroup:ArrayCollection = new ArrayCollection();
			
			private var prvVar4sqlRestriction:String = null;	
			
			
			private var prvVar4auWindow:Com_Unit_UnitGroup_AU = null;
			
			[Bindable]
			private var prvVar4blFunPermission4query:Boolean = true;			
			[Bindable]
			private var prvVar4blFunPermission4manage:Boolean = true;
			//	2	本地变量    end			
			
			//	3	公共方法    begin			
			//	3	公共方法    end		
			
			//	4	初始化方法    begin	
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				prvVar4blFunPermission4manage = ClientEnvironment.checkIsPermission( FunPermiConst.MAINDATA_UNIT_MANAGE );
				
			}			
			//	4	初始化方法    end				
			
			//	5	事件响应方法    begin		
			private function callbackFunction4query4comQCond():void
			{				
				this.prvVar4sqlRestriction = this.comQCond.pubFun4getParamOut4sqlRestriction();
				var blPageQuery:Boolean = this.comQCond.pubFun4getParamOut4blPageQuery();
				
				if( blPageQuery == true )
				{	
					UtilUi.pubFun4PopWaitWindow( this );
					this.srvUnitGroup.getUnitGroupsStat( this.prvVar4sqlRestriction );	
				}
				else
				{	
					//	不分页查询
					UtilUi.pubFun4PopWaitWindow( this );
					this.srvUnitGroup.getUnitGroups( this.prvVar4sqlRestriction, true, -1, ClientEnvironment.getSysParamPageSize() );
				}								
			}			
			
			//	分页查询相关		begin
			private function faultHandler4getUnitGroupsStat(event:FaultEvent):void
			{
				UtilUi.pubFun4RemoveWaitWindow();
				Alert.show( UtilExceptionLocale.getExceptionMessageLocaleByFaultEvent( event ), resourceManager.getString('alert','alt_title_warn') );
			}	
			
			private function resultHandler4getUnitGroupsStat(event:ResultEvent):void 
			{
				//	初始化数据表格控制板
				var rstSize:Number = Number(event.result);
				this.comAksDataGridControlBar.pubFun4setResultParams( true, rstSize, ClientEnvironment.getSysParamPageSize() );
				
				//	去执行第一页查询
				this.srvUnitGroup.getUnitGroups( this.prvVar4sqlRestriction, true, 1, ClientEnvironment.getSysParamPageSize() );
			}
			
			private function prvFun4pageQueryUnitGroups( _pageIndex:int ):void
			{
				UtilUi.pubFun4PopWaitWindow( this );
				this.srvUnitGroup.getUnitGroups( this.prvVar4sqlRestriction, true, _pageIndex, ClientEnvironment.getSysParamPageSize() );					
			}			
			//	分页查询相关		end
			
			
			private function faultHandler4getUnitGroups(event:FaultEvent):void
			{
				UtilUi.pubFun4RemoveWaitWindow();
				Alert.show( UtilExceptionLocale.getExceptionMessageLocaleByFaultEvent( event ), resourceManager.getString('alert','alt_title_warn') );
			}	
			
			private function resultHandler4getUnitGroups(event:ResultEvent):void 
			{
				this.prvVar4dp4comAdgUnitGroup = ArrayCollection(event.result);
				
				if( this.comQCond.pubFun4getParamOut4blPageQuery() == false )
				{
					//	不是分页查询，需要根据查询结果初始化数据表格控制板
					this.comAksDataGridControlBar.pubFun4setResultParams( false, this.prvVar4dp4comAdgUnitGroup.length, this.prvVar4dp4comAdgUnitGroup.length );
				}
				
				UtilUi.pubFun4RemoveWaitWindow();
			}					
			
			
			private function onNew():void
			{
				if( this.prvVar4auWindow == null )
				{
					this.prvVar4auWindow = Com_Unit_UnitGroup_AU(PopUpManager.createPopUp(this, Com_Unit_UnitGroup_AU, true));
					this.prvVar4auWindow.pubFun4setParamIn4closeCallback( this, callbackFunction4auWinClose );
				}
				else
				{
					PopUpManager.addPopUp( this.prvVar4auWindow, this, true );
				}	
				PopUpManager.centerPopUp(prvVar4auWindow);
				this.prvVar4auWindow.title = resourceManager.getString('menu','newUnitGroup');
				
				// 这里的代码须如此：先准备好后再设置给 auWindow
				// 如果直接设置一个新建对象给auWindow，然后再操作其准备其内容，则可能不能触发auWindow内部的相关绑定动作	
				var newUnitGroup:BUnitGroup = new BUnitGroup();					
				this.prvVar4auWindow.pubFun4setParamIo4unitGroup( newUnitGroup, UtilUi.AUWINDOW_USE_NEW );								
			} 
			
			private function onUpd():void
			{
				var selectedUnitGroup:BUnitGroup = BUnitGroup( this.comAdgUnitGroup.selectedItem );
				// 不必检查，这个方法被调用的前提已经保证 
				
				if( this.prvVar4auWindow == null )
				{
					this.prvVar4auWindow = Com_Unit_UnitGroup_AU(PopUpManager.createPopUp(this, Com_Unit_UnitGroup_AU, true));
					this.prvVar4auWindow.pubFun4setParamIn4closeCallback( this, callbackFunction4auWinClose );	
				}
				else
				{
					PopUpManager.addPopUp( this.prvVar4auWindow, this, true );
				}	
				
				PopUpManager.centerPopUp(prvVar4auWindow);
				this.prvVar4auWindow.title = resourceManager.getString('menu','editUnitGroup');
				
				// 这里的代码须如此：先准备好后再设置给 auWindow
				// 如果直接设置一个新建对象给auWindow，然后再操作其准备其内容，则可能不能触发auWindow内部的相关绑定动作	
				var updUnitGroup:BUnitGroup = new BUnitGroup();	
				updUnitGroup.copyDeep( selectedUnitGroup );			
				this.prvVar4auWindow.pubFun4setParamIo4unitGroup( updUnitGroup, UtilUi.AUWINDOW_USE_UPD );							 
				
			} 
			
			private function callbackFunction4auWinClose():void
			{
				if( this.prvVar4auWindow.pubFun4getParamOut4winCloseWay() == UtilUi.WINDOW_CLOSEWAY_OK )
				{
					var rstUnitGroup:BUnitGroup = this.prvVar4auWindow.pubFun4getParamIo4unitGroup();
					if( rstUnitGroup != null )
					{
						if( this.prvVar4auWindow.pubFun4getParamIo4winUse() == UtilUi.AUWINDOW_USE_NEW )
						{
							if( this.prvVar4dp4comAdgUnitGroup == null )
							{
								this.prvVar4dp4comAdgUnitGroup = new ArrayCollection();
							}
							this.prvVar4dp4comAdgUnitGroup.addItemAt( rstUnitGroup, 0 );							
						}
						else if( this.prvVar4auWindow.pubFun4getParamIo4winUse() == UtilUi.AUWINDOW_USE_UPD )
						{
							this.prvVar4dp4comAdgUnitGroup.setItemAt( rstUnitGroup, this.comAdgUnitGroup.selectedIndex );
						}
					}
				}
				//this.prvVar4auWindow = null;														
			}			
			
			
			private function onDel():void
			{
				var alert_title_hint:String = resourceManager.getString('alert','alert_title_hint');
				var alert_msg_deleteconfirm:String = resourceManager.getString('alert','alert_msg_delUnitGroupConfirm');
				
				Alert.show( alert_msg_deleteconfirm, alert_title_hint, Alert.OK | Alert.CANCEL, this, deleteConfirmHandler, null, Alert.OK );
				
			}				
			
			private function deleteConfirmHandler(event:CloseEvent):void
			{
				if( event.detail == Alert.OK )
				{
					// 相关检查都已在 prvFun4getEnabled4BtnDel中执行，此处不再检查    begin
					var selectedUnitGroup:BUnitGroup = BUnitGroup(this.comAdgUnitGroup.selectedItem);
					// 相关检查都已在 prvFun4getEnabled4BtnDel中执行，此处不再检查    end					
					UtilUi.pubFun4PopWaitWindow( this );
					this.srvUnitGroup.delUnitGroup( selectedUnitGroup );										
				}
			}	
			
			private function faultHandler4delUnitGroup(event:FaultEvent):void
			{
				UtilUi.pubFun4RemoveWaitWindow();
				
				Alert.show( UtilExceptionLocale.getExceptionMessageLocaleByFaultEvent( event ), resourceManager.getString('alert','alt_title_warn') );
			}	
			
			private function resultHandler4delUnitGroup(event:ResultEvent):void 
			{
				UtilUi.pubFun4RemoveWaitWindow();
				var alert_title_hint:String = resourceManager.getString('alert','alert_title_hint');
				var alert_msg:String = null;
				
				var blSucceed:Boolean = Boolean(event.result);
				if( blSucceed == true )
				{
					alert_msg = resourceManager.getString('alert','alert_msg_succeed');
					Alert.show(alert_msg, alert_title_hint);
					this.prvVar4dp4comAdgUnitGroup.removeItemAt( this.comAdgUnitGroup.selectedIndex );					
				}
				else
				{
					alert_msg = resourceManager.getString('alert','alert_msg_fail');
					Alert.show(alert_msg, alert_title_hint);
				}				
			}			
			//	5	事件响应方法    end
			
			//	6	本地方法    begin
			private function prvFun4getEnabled4BtnManage( _selectedItem:Object ):Boolean
			{
				// todo
				if( _selectedItem == null )
				{
					return false;
				}
				
				if( this.prvVar4blFunPermission4manage == false )
				{
					return false;
				}
				
				return true;
			}
			//	6	本地方法   end	
			
		]]>
	</fx:Script>
	

	<unit:Com_Unit_UnitGroup_QueryCond width="100%" id="comQCond" paramIn4queryCallback4thisObject="{this}" 
									   paramIn4queryCallback4function="{this.callbackFunction4query4comQCond}" 
									   paramIn4enabled="{this.prvVar4blFunPermission4query}">
	</unit:Com_Unit_UnitGroup_QueryCond>	
	
	<mx:VDividedBox width="100%" height="100%">
		<s:VGroup width="100%" height="100%">
			<common:LabelBoundary paddingTop="5" labelText="{resourceManager.getString('menu','unitGroup')}" />

			<containers:AksControlBar>
				<buttons:ButtonNew enabled="{this.prvVar4blFunPermission4manage}" click="onNew()"/>
				<buttons:ButtonEdit enabled="{this.prvFun4getEnabled4BtnManage(this.comAdgUnitGroup.selectedItem)}" click="onUpd()"/>
				<buttons:ButtonDelete enabled="{this.prvFun4getEnabled4BtnManage(this.comAdgUnitGroup.selectedItem)}" click="onDel()"/>
				<controls:AksDataGridControlBar id="comAksDataGridControlBar" width="100%" paramIn4callback4thisObject="{this}" paramIn4callback4queryFunction="{this.prvFun4pageQueryUnitGroups}" paramIn4dataGrid="{this.comAdgUnitGroup}">
				</controls:AksDataGridControlBar>
			</containers:AksControlBar>				
			<unit:Com_Unit_UnitGroup_AdvancedDataGrid width="100%" height="100%" itemDoubleClick="onUpd()" doubleClickEnabled="true" id="comAdgUnitGroup" dataProvider="{this.prvVar4dp4comAdgUnitGroup}"/>		
		</s:VGroup>
		<s:VGroup width="100%" height="100%">
			<common:LabelBoundary paddingTop="5" labelText="{resourceManager.getString('menu','unit')}" />

			<unit:Com_Unit_Unit_AdvancedDataGrid width="100%" height="100%" dataProvider="{BUnitGroup(this.comAdgUnitGroup.selectedItem).units}"/>
		</s:VGroup>
		
	</mx:VDividedBox>
</mx:Module>
