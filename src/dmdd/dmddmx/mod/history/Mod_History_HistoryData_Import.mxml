<?xml version="1.0" encoding="utf-8"?>
<mx:Module xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:s="library://ns.adobe.com/flex/spark" 
		   xmlns:mx="library://ns.adobe.com/flex/mx" layout="vertical" width="100%" height="100%" paddingLeft="10" paddingRight="10"
		   creationComplete="creationCompleteHandler(event)" keyDown="dataGridKeyDownHandler(event)" 
		   xmlns:common="dmdd.dmddmx.com.common.*"
		   xmlns:containers="aks.aksmx.containers.*" xmlns:history="dmdd.dmddmx.com.history.*" xmlns:bizdata="dmdd.dmddmx.com.bizdata.*" xmlns:unit="dmdd.dmddmx.com.unit.*">
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
		<s:RemoteObject endpoint="{ClientEnvironment.endpoint}" id="srvHistory" destination="HistoryService" requestTimeout="-1">	
			<s:method name="saveHistoryDatas4ImportUI" result="resultHandler4saveHistoryDatas4ImportUI(event)" fault="faultHandler4saveHistoryDatas4ImportUI(event)">
			</s:method>							  
		</s:RemoteObject>			
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import aks.aksas.utils.UtilUi;
			
			import dmdd.dmddas.common.constant.BizConst;
			import dmdd.dmddas.common.constant.SysConst;
			import dmdd.dmddas.common.system.ClientEnvironment;
			import dmdd.dmddas.common.utils.UtilExceptionLocale;
			import dmdd.dmddas.common.utils.UtilPeriod;
			import dmdd.dmddas.dataaccess.aidobject.ABImHistoryData;
			import dmdd.dmddas.dataaccess.bizobject.BBizData;
			import dmdd.dmddas.dataaccess.bizobject.BSysDictionaryItem;
			import dmdd.dmddas.dataaccess.bizobject.BUnit;
			import dmdd.dmddas.dataaccess.bizobject.BUnitGroup;
			import com.cool.assets.CoolAssetsFile;

			import mx.collections.ArrayCollection;
			import mx.collections.IViewCursor;
			import mx.controls.Alert;
			import mx.controls.DataGrid;
			import mx.controls.dataGridClasses.DataGridColumn;
			import mx.events.FlexEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			
			
			//	描述    begin
			//	描述    end
			
			//	0	输入参数    begin						
			//	0	输入参数    end
			
			//	1	输出参数    begin
			//	1	输出参数    end
			
			//	2	本地变量    begin	
			[Bindable]
			private var prvVar4dp4comAdgImHistoryData:ArrayCollection = new ArrayCollection();
			[Bindable]
			private var prvVar4arrDictBizDataType:ArrayCollection = new ArrayCollection();		
			
			private var prvVar4unit:BUnit = null;
			
			private var prvVar4arrFailedRecord:ArrayCollection = new ArrayCollection();
			
			private var prvVar4arrOkButFailedRecord:ArrayCollection = new ArrayCollection();
			//	2	本地变量    end			
			
			//	3	公共方法    begin			
			//	3	公共方法    end		
			
			//	4	初始化方法    begin	
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				//	初始化界面参数	begin
				this.prvVar4arrDictBizDataType = new ArrayCollection();
				
				var arrDictBizDataType:ArrayCollection = ClientEnvironment.getBizDataTypeArr();
				if( arrDictBizDataType != null )
				{
					var i:int;
					var sysDictionaryItem:BSysDictionaryItem = null;
					for( i=0; i<arrDictBizDataType.length; i=i+1 )
					{
						sysDictionaryItem = BSysDictionaryItem(arrDictBizDataType.getItemAt(i));
						if( sysDictionaryItem.value == BizConst.BIZDATA_TYPE_HISTORY )
						{
							this.prvVar4arrDictBizDataType.addItem( sysDictionaryItem );
						}
					}
				}				
				//	初始化界面参数	end				
			}			
			//	4	初始化方法    end				
			
			//	5	事件响应方法    begin
			private function onChange4comboBoxUnit():void
			{
				var selectedUnit:BUnit = BUnit(this.comboBoxUnit.selectedItem)
				var i:int;
				var imHistoryData:ABImHistoryData = null;
				if( selectedUnit != null && this.prvVar4unit != null && selectedUnit.id != this.prvVar4unit.id )
				{
					if( this.prvVar4dp4comAdgImHistoryData != null )
					{
						for( i=0; i<this.prvVar4dp4comAdgImHistoryData.length; i=i+1 )
						{
							imHistoryData = ABImHistoryData(this.prvVar4dp4comAdgImHistoryData.getItemAt(i));
							imHistoryData.exchangeUnit( this.prvVar4unit.exchangeRate, selectedUnit.exchangeRate );
						}
					}	
				}
					
				this.prvVar4unit = selectedUnit;				
			}
			
			private function onClear():void
			{
				this.prvVar4dp4comAdgImHistoryData = new ArrayCollection();
				this.comAdgImHistoryData.columnImportResult.visible = false;
				this.comAdgImHistoryData.columnWarnInfo.visible=false;
				this.btnSave.enabled = false;
				this.btnExport.enabled = false;
				this.btnExportWarnInfo.enabled = false;
			}
			
			private function onSave():void
			{
				var alert_title_hint:String = resourceManager.getString('alert','alert_title_hint');
				var alert_msg:String = null;  
				
				var bizData:BBizData = this.comBizDataField.paramIo4selectedBizData;
				if( bizData == null )
				{
					alert_msg = resourceManager.getString('alert','alert_msg_nobizdata12');
					Alert.show( alert_msg, alert_title_hint );	
					return;							
				}
				
				var unitGroup:BUnitGroup = this.comUnitGroupField.paramIo4selectedUnitGroup;
				if( unitGroup == null )
				{
					alert_msg = resourceManager.getString('alert','alert_msg_nounitgroup12');
					Alert.show( alert_msg, alert_title_hint );	
					return;						
				}
				
				var selectedUnit:BUnit = BUnit(this.comboBoxUnit.selectedItem);
				if( selectedUnit == null )
				{
					alert_msg = resourceManager.getString('alert','alert_msg_nounit12');
					Alert.show( alert_msg, alert_title_hint );	
					return;						
				}				
				
				if( this.prvVar4dp4comAdgImHistoryData.length <= 0 )
				{
					alert_msg = resourceManager.getString('alert','alert_msg_nodatatosave12');
					Alert.show( alert_msg, alert_title_hint );	
					return;												
				}
				
				UtilUi.pubFun4PopWaitWindow( this );
				
				//	传递到服务器的数据应该是以基准单位计数的	begin
				var arrImHistoryData4save:ArrayCollection = new ArrayCollection();
				var i:int;
				var imHistoryData:ABImHistoryData = null;
				var imHistoryData_new:ABImHistoryData = null;
				for( i=0; i<this.prvVar4dp4comAdgImHistoryData.length; i=i+1 )
				{
					imHistoryData = ABImHistoryData(this.prvVar4dp4comAdgImHistoryData.getItemAt(i));
					imHistoryData_new = new ABImHistoryData();
					imHistoryData_new.copyDeep( imHistoryData );
					if( selectedUnit.isBase == BizConst.GLOBAL_YESNO_NO )
					{
						imHistoryData_new.exchangeUnit( selectedUnit.exchangeRate, 1 );
					}	
					arrImHistoryData4save.addItem( imHistoryData_new );
					
				}									
				//	传递到服务器的数据应该是以基准单位计数的	end
				
				this.srvHistory.saveHistoryDatas4ImportUI( bizData, unitGroup, arrImHistoryData4save );
			}	
			
			private function faultHandler4saveHistoryDatas4ImportUI(event:FaultEvent):void
			{
				UtilUi.pubFun4RemoveWaitWindow();
				Alert.show( UtilExceptionLocale.getExceptionMessageLocaleByFaultEvent( event ), resourceManager.getString('alert','alt_title_warn') );
			}
			
			private function resultHandler4saveHistoryDatas4ImportUI(event:ResultEvent):void 
			{
				this.prvVar4dp4comAdgImHistoryData = ArrayCollection(event.result);
				
				this.prvVar4arrFailedRecord = new ArrayCollection();
				this.prvVar4arrOkButFailedRecord = new ArrayCollection();
				var totalNum:int = 0; 
				var failNum:int = 0;
				var i:int;
				var rstImHistoryData:ABImHistoryData = null;
				if( this.prvVar4dp4comAdgImHistoryData != null )
				{
					totalNum = this.prvVar4dp4comAdgImHistoryData.length;
					for( i=0; i<this.prvVar4dp4comAdgImHistoryData.length; i=i+1 )
					{
						rstImHistoryData = ABImHistoryData(this.prvVar4dp4comAdgImHistoryData.getItemAt(i));
						if( rstImHistoryData.importResult != BizConst.IMPORT_RESULT_SUCCESS )
						{
							rstImHistoryData.importResult = UtilExceptionLocale.getExceptionMessageLocaleByCauseMessage( rstImHistoryData.importResult );
							rstImHistoryData.warnInfo =UtilExceptionLocale.getExceptionMessageLocaleByCauseMessage( rstImHistoryData.warnInfo ); 
							this.prvVar4arrFailedRecord.addItem( rstImHistoryData );
						}
						else
						{
							if(rstImHistoryData.warnInfo!="")
							{
								rstImHistoryData.warnInfo =UtilExceptionLocale.getExceptionMessageLocaleByCauseMessage( rstImHistoryData.warnInfo );
								prvVar4arrOkButFailedRecord.addItem(rstImHistoryData);
							}							
						}
					}
					failNum = this.prvVar4arrFailedRecord.length;
				}
				
				UtilUi.pubFun4RemoveWaitWindow();
				this.comAdgImHistoryData.columnImportResult.visible = true;
				this.comAdgImHistoryData.columnWarnInfo.visible=true;
				
				var alert_title_hint:String = resourceManager.getString('alert','alert_title_hint');
				var alert_msg:String = null;  	
				
				alert_msg = resourceManager.getString('alert','alert_msg_totalnum12');
				alert_msg = alert_msg + totalNum + "\n";
				alert_msg = alert_msg + resourceManager.getString('alert','alert_msg_failnum12');
				alert_msg = alert_msg + failNum + "\n";
				
				Alert.show( alert_msg, alert_title_hint );
				
				if( failNum > 0 )
				{
					this.btnExport.enabled = true;					
				}
				else
				{
					this.btnExport.enabled = false;					
				}		
				
				if(prvVar4arrOkButFailedRecord.length>0)
				{
					this.btnExportWarnInfo.enabled = true;
				}
				else
				{
					this.btnExportWarnInfo.enabled = false;
				}
			}		
			private function onExport():void
			{
				//	只导出错误的，所以先把组件显示的内容换成错误的，处理完后再换回来	begin
				var arrdp4comAdgImHistoryData_bak:ArrayCollection = new ArrayCollection();
				arrdp4comAdgImHistoryData_bak.addAll( this.prvVar4dp4comAdgImHistoryData );
				
				this.prvVar4dp4comAdgImHistoryData.removeAll();
				this.prvVar4dp4comAdgImHistoryData.addAll( this.prvVar4arrFailedRecord );
			//	this.prvVar4dp4comAdgImHistoryData.addAll(this.prvVar4arrOkButFailedRecord);
				
				var TSVString:String = this.exportCSV( this.comAdgImHistoryData ) ;
				
				this.prvVar4dp4comAdgImHistoryData.removeAll();
				this.prvVar4dp4comAdgImHistoryData.addAll( arrdp4comAdgImHistoryData_bak );
				//	只导出错误的，所以先把组件显示的内容换成错误的，处理完后再换回来	end
						
				// Copy the TSV string to the clipboard
				System.setClipboard(TSVString);					
			}
			
			private function onExportWarnInfo():void
			{
				//	只导出错误的，所以先把组件显示的内容换成错误的，处理完后再换回来	begin
				var arrdp4comAdgImHistoryData_bak:ArrayCollection = new ArrayCollection();
				arrdp4comAdgImHistoryData_bak.addAll( this.prvVar4dp4comAdgImHistoryData );
				
				this.prvVar4dp4comAdgImHistoryData.removeAll();
				//this.prvVar4dp4comAdgImHistoryData.addAll( this.prvVar4arrFailedRecord );
				this.prvVar4dp4comAdgImHistoryData.addAll(this.prvVar4arrOkButFailedRecord);
				
				var TSVString:String = this.exportCSV( this.comAdgImHistoryData ) ;
				
				this.prvVar4dp4comAdgImHistoryData.removeAll();
				this.prvVar4dp4comAdgImHistoryData.addAll( arrdp4comAdgImHistoryData_bak );
				//	只导出错误的，所以先把组件显示的内容换成错误的，处理完后再换回来	end
				
				// Copy the TSV string to the clipboard
				System.setClipboard(TSVString);	
			}
			
			//	5	事件响应方法    end
			
			//	6	本地方法    begin
			private function exportCSV(dg:DataGrid, csvSeparator:String="\t", lineSeparator:String="\n"):String
			{
				var data:String = "";
				var columns:Array = dg.columns;
				var columnCount:int = columns.length;
				var column:DataGridColumn;
				var header:String = "";
				var headerGenerated:Boolean = false;
				var dataProvider:Object = dg.dataProvider;
				var rowCount:int = dataProvider.length;
				var dp:Object = null;
				
				var cursor:IViewCursor = dataProvider.createCursor ();
				var j:int = 0;
				
				//loop through rows
				while (!cursor.afterLast)
				{
					var obj:Object = null;
					obj = cursor.current;
					
					//loop through all columns for the row
					for(var k:int = 0; k < columnCount; k++)
					{
						column = columns[k];
						//Exclude column data which is invisible (hidden)
						if(!column.visible)
						{
							continue;
						}
						
						data += "\""+ column.itemToLabel(obj)+ "\"";
						if(k < (columnCount -1))
						{
							data += csvSeparator;
						}
						//generate header of CSV, only if it's not genereted yet
						if (!headerGenerated)
						{
							var columnHeadStr:String = column.headerText;
							
							header += "\"" + columnHeadStr.replace("\n","") + "\""; //把表头中的换行去掉
							if (k < columnCount - 1)
							{
								header += csvSeparator;
							}
						}
						
						
					}
					
					headerGenerated = true;
					if (j < (rowCount - 1))
					{
						data += lineSeparator;
					}
					j++;
					cursor.moveNext ();
				}
				
				//set references to null:
				dataProvider = null;
				columns = null;
				column = null;
				
				return (header + "\r\n" + data);
			} 
			

			
			private function dataGridKeyDownHandler( event:KeyboardEvent ):void   
			{     
				if (event.ctrlKey && this.comAdgImHistoryData.getChildByName("clipboardProxy") == null )     
				{       
					// Add an invisible TextField object to the DataGrid       
					var textFieldClipboard:TextField = new TextField();       
					textFieldClipboard.name = "clipboardProxy";       
					this.comAdgImHistoryData.addChild(textFieldClipboard);       
					textFieldClipboard.visible = false;       
					textFieldClipboard.type = TextFieldType.INPUT;       
					textFieldClipboard.multiline = true;       
					// Populate the TextField with selected data in TSV format       
					// textField.text = getTextFromItems(dataGrid.selectedItems);       
					// textField.setSelection(0, textField.text.length - 1);       
					// Listen for textInput event       
					textFieldClipboard.addEventListener(TextEvent.TEXT_INPUT, clipboardProxyPasteHandler);    
					// Set player-level focus to the TextField       
					systemManager.stage.focus = textFieldClipboard;     
				}   
			}
			
			private function clipboardProxyPasteHandler(event:TextEvent):void   
			{     
				// Extract values from TSV format and populate the DataGrid
				if( this.comAdgImHistoryData.columnImportResult.visible == true )
				{
					this.comAdgImHistoryData.columnImportResult.visible = false;
				}
				
				this.prvVar4dp4comAdgImHistoryData = new ArrayCollection();
				this.getImHistoryDatasFromText( event.text );  
				this.btnSave.enabled = true;
				
				// 一定要在这里把 textField移除，不然会报 Flash.events.event 到 indexChang eEvent 的类型转换错误
				// 移去 textField
				var textFieldClipboard:TextField = TextField(this.comAdgImHistoryData.getChildByName("clipboardProxy"));       
				if ( textFieldClipboard != null )
				{         
					this.comAdgImHistoryData.removeChild(textFieldClipboard);
				}   								
			}   
			
			private function getImHistoryDatasFromText(text:String):void   
			{     
				var alert_title_hint:String = resourceManager.getString('alert','alert_title_hint');
 				var alert_msg:String = null;  
				
				var rstArrayImHistoryData:Array = new Array();
				var rows:Array = text.split("\n");
				if( rows == null )
				{
					alert_msg = resourceManager.getString('alert','alert_msg_nohistorydata12');
					Alert.show( alert_msg, alert_title_hint );	
					return;	
				}     
				if (!rows[rows.length - 1])
				{       
					rows.pop();
				}     
				
				if( rows.length <= 1 )
				{
					alert_msg = resourceManager.getString('alert','alert_msg_nohistorydata12');
					Alert.show( alert_msg, alert_title_hint );						
					return;
				}
				
				var rwHeader:String = rows[0];
				var fieldsHeader:Array = rwHeader.split( "\t" );
				if(!(fieldsHeader[fieldsHeader.length-1]))
				{
					fieldsHeader.pop();
				} 				
				if( fieldsHeader.length <= 2 )
				{
					alert_msg = resourceManager.getString('alert','alert_msg_nohistorydatacolumen12');
					Alert.show( alert_msg, alert_title_hint );						
					return;					
				}
				
				var colIdx:int;
				var periodBegin:int = UtilPeriod.parse( fieldsHeader[2] );
				var period:int = SysConst.PERIOD_NULL;
				if( periodBegin == SysConst.PERIOD_NULL )
				{
					alert_msg = resourceManager.getString('alert','alert_msg_errorhistorydatacolumnheader12');
					Alert.show( alert_msg, alert_title_hint );
					return;																	
				}
				for( colIdx = 3; colIdx < fieldsHeader.length; colIdx=colIdx+1 )
				{
					period = UtilPeriod.parse( fieldsHeader[ colIdx ] );
					if( period == SysConst.PERIOD_NULL )
					{
						alert_msg = resourceManager.getString('alert','alert_msg_errorhistorydatacolumnheader12');
						Alert.show( alert_msg, alert_title_hint );
						return;												
					}
					if( UtilPeriod.getPeriodDifference( periodBegin, period ) != (colIdx - 2) )
					{
						alert_msg = resourceManager.getString('alert','alert_msg_historydatacolumnheaderperiodnotsucceed12');
						Alert.show( alert_msg, alert_title_hint );
						return;																		
					}
				}
				var periodEnd:int = period;
				if( periodEnd == SysConst.PERIOD_NULL )
				{
					periodEnd = periodBegin;	
				}
				if( UtilPeriod.getPeriodDifference(periodEnd, ClientEnvironment.getSysPeriod().historyOpenPeriod) < 0 )
				{
					alert_msg = resourceManager.getString('alert','alert_msg_periodendgtopen12');
					Alert.show( alert_msg, alert_title_hint );
					return;																							
				}
				
				var periodDiff:int = UtilPeriod.getPeriodDifference(periodBegin, periodEnd);
				if( periodDiff >= SysConst.PERIODNUM_MAX_NUM )
				{
					alert_msg = resourceManager.getString('alert','alter_msg_perioddiffover12');
					Alert.show( alert_msg, alert_title_hint );
					return;																							
				}
				
				this.comAdgImHistoryData.pubFun4setParamIo4periodBeginEnd( periodBegin, periodEnd, true );
				
				var row:String = null;
				var rowIdx:int;
				var fields:Array = null;
				var periodIdx:int;
				var periodNum:int;
				
				var newImHistoryData:ABImHistoryData = null;
				for( rowIdx=1; rowIdx<rows.length; rowIdx=rowIdx+1 )
				{
					newImHistoryData = new ABImHistoryData();
					newImHistoryData.periodBegin = periodBegin;
					newImHistoryData.periodEnd = periodEnd;
					newImHistoryData.importResult = null;
					
					row = rows[rowIdx];
					fields = row.split("\t");
					if( fields.length <= 2 )
					{
						continue;
					}
					newImHistoryData.productCode = fields[0];
					newImHistoryData.organizationCode = fields[1];
					
					periodNum = Math.min( periodDiff+1, fields.length-2 );
					for( periodIdx=0; periodIdx < periodNum; periodIdx=periodIdx+1 )
					{
						newImHistoryData.pubFun4setPeriodDataValue( periodIdx, Number( fields[2+periodIdx] ) );	
					}
					
					this.prvVar4dp4comAdgImHistoryData.addItem( newImHistoryData );
				}
			}   			
			//	6	本地方法   end	
			
		]]>
	</fx:Script>
	<common:LabelBoundary paddingTop="5" labelText="{resourceManager.getString('menu','historyImp')}" />
	<containers:AksControlBar>
		<s:Label text="{resourceManager.getString('note','busData')}"/>
		<bizdata:Com_BizData_BizData_Field id="comBizDataField" paramIn4arrDictBizDataType="{this.prvVar4arrDictBizDataType}" paramIn4blConstrainedByPermission="true" paramIn4blOnlyIsManaging="true" width="100%">
		</bizdata:Com_BizData_BizData_Field>
		<mx:Spacer width="20"/>
		<s:Label text="{resourceManager.getString('note','unitGroup')}"/>
		<unit:Com_Unit_UnitGroup_Field id="comUnitGroupField" width="100%">
		</unit:Com_Unit_UnitGroup_Field>
		<mx:Spacer width="20"/>
		<s:Label text="{resourceManager.getString('note','unit')}"/>
		<s:ComboBox id="comboBoxUnit" dataProvider="{this.comUnitGroupField.paramIo4selectedUnitGroup.units}" selectedIndex="0" change="onChange4comboBoxUnit()"/>
		<mx:Spacer width="50"/>
		<s:Button label="{resourceManager.getString('button','save')}" icon="{CoolAssetsFile.saveIcon}" click="onSave()" id="btnSave" enabled="false"/> 
		<s:Button label="{resourceManager.getString('button','clear')}" icon="{CoolAssetsFile.clearIcon}" click="onClear()"/>
		<s:Button label="{resourceManager.getString('button','expLogFailed')}" icon="{CoolAssetsFile.doctableIcon}" click="onExport()" id="btnExport" enabled="false"/>
		<s:Button label="{resourceManager.getString('button','alarmRecExp')}" icon="{CoolAssetsFile.warnIcon}" click="onExportWarnInfo()" id="btnExportWarnInfo" enabled="false"/>
	</containers:AksControlBar>
	<history:Com_History_ImHistoryData_AdvancedDataGrid id="comAdgImHistoryData" dataProvider="{this.prvVar4dp4comAdgImHistoryData}" width="100%" height="100%"/>

</mx:Module>
